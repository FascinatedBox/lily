import (Interpreter,
        TestCase) "../t/testing"

class TestBadSyntax < TestCase
{
    public define test_bad_assign_type
    {
        var t = Interpreter()

        # bad assign type (to global var)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'String' to type 'Integer'.
                from [test]:2:
        """,
        """\
            var a = 10
            a = "10"
        """)

        # bad assign type (to class property)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'String' to type 'Integer'.
                from [test]:6:
        """,
        """\
            class Example {
                public var @a = 10
            }

            var v = Example()
            v.a = "5"
        """)

        # bad assign type (to upvalue)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'String' to type 'Integer'.
                from [test]:3:
        """,
        """\
            define f(a: Integer) {
                define g {
                    a = ""
                }
            }
        """)

        # bad assign type (to subscript)

        assert_parse_fails(t, """\
            SyntaxError: Right side of assignment is incomplete type 'Option[?]'.
                from [test]:3:
        """,
        """\
            var v = [1, 2, 3]

            v[0] = None
        """)

        # bad assign type (to string subscript)

        assert_parse_fails(t, """\
            SyntaxError: Subscript assignment on a String is not allowed.
                from [test]:3:
        """,
        """\
            var v = "asdf"

            v[0] = 1
        """)
    }

    public define test_bad_blocks
    {
        var t = Interpreter()

        # bad blocks (bad if expression case)

        assert_parse_fails(t, """\
            SyntaxError: Tuple[Integer, String] is not a valid condition type.

               |
             1 | if <[1, "2"]>: {
               |    ^

                from [test]:1:
        """,
        """\
            if <[1, "2"]>: {
            }
        """)

        # bad blocks (bad or/and expression left only)

        assert_parse_fails(t, """\
            SyntaxError: Unit is not a valid condition type.

               |
             1 | if unit || 1: {
               |    ^

                from [test]:1:
        """,
        """\
            if unit || 1: {
            }
        """)

        # bad blocks (bad or/and expression right only)

        assert_parse_fails(t, """\
            SyntaxError: Unit is not a valid condition type.

               |
             1 | if 1 || unit: {
               |         ^

                from [test]:1:
        """,
        """\
            if 1 || unit: {
            }
        """)

        # bad blocks (bad or/and expression middle)

        assert_parse_fails(t, """\
            SyntaxError: Unit is not a valid condition type.

               |
             1 | if 1 || unit && 1: {
               |         ^

                from [test]:1:
        """,
        """\
            if 1 || unit && 1: {
            }
        """)

        # bad blocks (bad or/and expression middle all or)

        assert_parse_fails(t, """\
            SyntaxError: Unit is not a valid condition type.

               |
             1 | if 1 || unit || 1: {
               |         ^

                from [test]:1:
        """,
        """\
            if 1 || unit || 1: {
            }
        """)

        # bad blocks (for loop var must be Integer)

        assert_parse_fails(t, """\
            SyntaxError: For loop index must be Integer, but i has type String.

               |
             2 | for i in 0...10: {}
               |           ^

                from [test]:2:
        """,
        """\
            var i = ""
            for i in 0...10: {}
        """)

        # bad blocks (for must start with Integer)

        assert_parse_fails(t, """\
            SyntaxError: Expected type 'Integer', but got type 'String'.

               |
             1 | for i in "a"...0: {
               |             ^

                from [test]:1:
        """,
        """\
            for i in "a"...0: {
            }
        """)

        # bad blocks (condition using assign)

        assert_parse_fails(t, """\
            SyntaxError: Expected a value, but got an assignment instead.

               |
             2 | if v = 5:
               |         ^

                from [test]:2:
        """,
        """\
            var v = 10
            if v = 5:
                v = 1

            raise Exception("Failed.")
        """)

        # bad blocks (while after do)

        assert_parse_fails(t, """\
            SyntaxError: Expected 'while' here.

               |
             2 | } asdf
               |   ^

                from [test]:2:
        """,
        """\
            do: {
            } asdf
        """)

        # bad blocks (do while not allowed to use scope vars in condition)

        assert_parse_fails(t, """\
            SyntaxError: v has not been declared.

               |
             4 | } while v == 10
               |         ^

                from [test]:4:
        """,
        """\
            do: {
                var v = 10
                continue
            } while v == 10
        """)

        # bad blocks (unterminated if)

        assert_parse_fails(t, """\
            SyntaxError: Reached end of file while still inside a block.
                from [test]:2:
        """,
        """\
            if 1: {
        """)

        # bad blocks (else after else)

        assert_parse_fails(t, """\
            SyntaxError: else in exhaustive if.

               |
             5 | else:
               | ^

                from [test]:5:
        """,
        """\
            if 1: {
                1
            else:
                1
            else:
                1
            }
        """)

        # bad blocks (elif after else)

        assert_parse_fails(t, """\
            SyntaxError: elif in exhaustive if.

               |
             5 | elif 1:
               | ^

                from [test]:5:
        """,
        """\
            if 1: {
                1
            else:
                1
            elif 1:
                1
            }
        """)
    }

    public define test_bad_coercion
    {
        var t = Interpreter()

        # bad coercion (Negative Integer for Byte)

        assert_parse_fails(t, """\
            SyntaxError: Value -1 is out of range for a Byte.
                from [test]:1:
        """,
        """\
            var a: Byte = -1
        """)

        # bad coercion (Out of range Integer for Byte)

        assert_parse_fails(t, """\
            SyntaxError: Value 256 is out of range for a Byte.
                from [test]:1:
        """,
        """\
            var a: Byte = 256
        """)
    }

    public define test_bad_cast
    {
        var t = Interpreter()

        # bad cast (from Integer to Tuple[Integer])

        assert_parse_fails(t, """\
            SyntaxError: Cannot cast type 'Integer' to type 'Tuple[Integer]'.
                from [test]:1:
        """,
        """\
            var v = 1.@(Tuple[Integer])
        """)
    }

    public define test_bad_comment
    {
        var t = Interpreter()

        # bad comment (unterminated multi-line)

        assert_parse_fails(t, """\
            SyntaxError: Unterminated multi-line comment (started at line 2).
                from [test]:6:
        """,
        """\

        #[



        """)

        # bad comment (docblock with double pound)

        assert_parse_fails(t, """\
            SyntaxError: Docblock line does not start with full '###'.

               |
             2 | ##
               | ^

                from [test]:2:
        """,
        """\
        ###
        ##
            define f {}
        """)

        # bad comment (docblock inconsistent indent)

        assert_parse_fails(t, """\
            SyntaxError: Docblock has inconsistent indentation.

               |
             2 | ### 1234
               | ^

                from [test]:2:
        """,
        """\
        ### asdf
            ### 1234
            define f {}
        """)

        # bad comment (docblock not followed by function or class)

        assert_parse_fails(t, """\
            SyntaxError: A docblock is not allowed here.
                from [test]:7:
        """,
        """
        var v = 10

        ### Example documentation block
        ###
        ### More details follow
        """)

        # bad comment (docblock preceded by non-whitespace).

        assert_parse_fails(t, """\
            SyntaxError: Docblock is preceded by non-whitespace.

               |
             2 | 0 ### blah
               | ^

                from [test]:2:
        """,
        """
        0 ### blah
        ### blah
        ### blah
        """)

        # bad comment (docblock on nested definition)

        assert_parse_fails(t, """\
            SyntaxError: Docblocks are only allowed on toplevel symbols.

               |
             3 | define g {
               | ^

                from [test]:3:
        """,
        """\
            define f {
                ### doc
                define g {

                }
            }
        """)

        # bad comment (docblock on var in anon block)

        assert_parse_fails(t, """\
            SyntaxError: Docblocks are only allowed on toplevel symbols.

               |
             3 | var v = 10
               | ^

                from [test]:3:
        """,
        """\
            {
                ### doc
                var v = 10
            }
        """)
    }

    public define test_bad_ctor
    {
        var t = Interpreter()

        # bad ctor (no ctor for Integer)

        assert_parse_fails(t, """\
            SyntaxError: Class Integer does not have a constructor.

               |
             1 | var v = Integer(10)
               |                ^

                from [test]:1:
        """,
        """\
            var v = Integer(10)
        """)
    }

    public define test_bad_decl
    {
        var t = Interpreter()

        # bad decl (declare var twice)

        assert_parse_fails(t, """\
            SyntaxError: v has already been declared.

               |
             2 | var v = 10
               |     ^

                from [test]:2:
        """,
        """\
            var v = 10
            var v = 10
        """)

        # bad decl (declare define twice)

        assert_parse_fails(t, """\
            SyntaxError: f has already been declared.

               |
             2 | define f {}
               |        ^

                from [test]:2:
        """,
        """\
            define f {}
            define f {}
        """)

        # bad decl (declare constant twice)

        assert_parse_fails(t, """\
            SyntaxError: c has already been declared.

               |
             2 | constant c = 10
               |          ^

                from [test]:2:
        """,
        """\
            constant c = 10
            constant c = 10
        """)

        # bad decl (generic class with not enough types)

        assert_parse_fails(t, """\
            SyntaxError: Class Container expects 2 type(s), but got 1 type(s).

               |
             2 | var v: Container[Integer] = Container()
               |                         ^

                from [test]:2:
        """,
        """\
            class Container[A, B] {}
            var v: Container[Integer] = Container()
        """)

        # bad decl (generic class with too many types)

        assert_parse_fails(t, """\
            SyntaxError: Class Container expects 1 type(s), but got 3 type(s).

               |
             2 | var v: Container[Integer, Integer, Integer] = Container()
               |                                           ^

                from [test]:2:
        """,
        """\
            class Container[A] {}
            var v: Container[Integer, Integer, Integer] = Container()
        """)
    }

    public define test_bad_literal
    {
        var t = Interpreter()

        # bad literal (Integer too large)

        assert_parse_fails(t, """\
            SyntaxError: Integer value is too large.

               |
             1 | var v = 9223372036854775809
               |         ^

                from [test]:1:
        """,
        """\
            var v = 9223372036854775809
        """)

        # bad literal (Integer too small)

        assert_parse_fails(t, """\
            SyntaxError: Integer value is too small.

               |
             1 | var v = -9223372036854775809
               |         ^

                from [test]:1:
        """,
        """\
            var v = -9223372036854775809
        """)

        # bad literal (Double too large)

        assert_parse_fails(t, """\
            SyntaxError: Double value is out of range.

               |
             1 | var v = 9e999
               |         ^

                from [test]:1:
        """,
        """\
            var v = 9e999
        """)

        # bad literal (Double with Byte suffix)

        assert_parse_fails(t, """\
            SyntaxError: Double value with Byte suffix.

               |
             1 | var v = 1.1t
               |         ^

                from [test]:1:
        """,
        """\
            var v = 1.1t
        """)

        # bad literal (invalid exponent)

        assert_parse_fails(t, """\
            SyntaxError: Expected a base 10 number after exponent.

               |
             1 | var v = 1e?
               |         ^

                from [test]:1:
        """,
        """\
            var v = 1e?
        """)

        # bad literal (exponent too large)

        assert_parse_fails(t, """\
            SyntaxError: Exponent is too large.

               |
             1 | var v = 1e3000
               |         ^

                from [test]:1:
        """,
        """\
            var v = 1e3000
        """)

        # bad literal (Byte with - sign)

        assert_parse_fails(t, """\
            SyntaxError: Byte values cannot have a sign.

               |
             1 | var v = -1t
               |         ^

                from [test]:1:
        """,
        """\
            var v = -1t
        """)

        # bad literal (Byte with + sign)

        assert_parse_fails(t, """\
            SyntaxError: Byte values cannot have a sign.

               |
             1 | var v = +1t
               |         ^

                from [test]:1:
        """,
        """\
            var v = +1t
        """)

        # bad literal (Byte too large)

        assert_parse_fails(t, """\
            SyntaxError: Byte value is too large.

               |
             1 | var v = 256t
               |         ^

                from [test]:1:
        """,
        """\
            var v = 256t
        """)

        # bad literal (Byte quote literal not multi-byte)

        assert_parse_fails(t, """\
            SyntaxError: Multi-character byte literal.

               |
             1 | var v = '12'
               |         ^

                from [test]:1:
        """,
        """\
            var v = '12'
        """)

        # bad literal (Byte quote literal empty)

        assert_parse_fails(t, """\
            SyntaxError: Byte literals cannot be empty.

               |
             1 | var v = ''
               |         ^

                from [test]:1:
        """,
        """\
            var v = ''
        """)

        # bad literal (invalid Byte escape)

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | var v = '\\?'
               |         ^

                from [test]:1:
        """,
        """\
            var v = '\\?'
        """)

        # bad literal (invalid String escape)

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | var v = "\\?"
               |         ^

                from [test]:1:
        """,
        """\
            var v = "\\?"
        """)

        # bad literal (unterminated multi-line String)

        assert_parse_fails(t, """\
            SyntaxError: Unterminated string (started at line 3).
                from [test]:6:
        """,
        """\


            var v = \"\"\"


        """)

        # bad literal (newline in single-line String)

        assert_parse_fails(t, """\
            SyntaxError: Newline in single-line string.

               |
             1 | var v = "
               |         ^

                from [test]:1:
        """,
        """\
            var v = "
        """)

        # bad literal (zero escape on String literal)

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | var v = "\\0"
               |         ^

                from [test]:1:
        """,
        """\
            var v = "\\0"
        """)

        # bad literal (high-bit escape on String literal)

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | var v = "\\128"
               |         ^

                from [test]:1:
        """,
        """\
            var v = "\\128"
        """)

        # bad literal (escape over 255).

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | var v = B"\\256"
               |         ^

                from [test]:1:
        """,
        """\
            var v = B"\\256"
        """)

        # bad literal (invalid hex escape for String).

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | "\\xFF"
               | ^

                from [test]:1:
        """,
        """\
            "\\xFF"
        """)

        # bad literal (short hex escape).

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | "\\x"
               | ^

                from [test]:1:
        """,
        """\
            "\\x"
        """)

        # bad literal (empty hex escape).

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | "\\xF"
               | ^

                from [test]:1:
        """,
        """\
            "\\xF"
        """)

        # bad literal (invalid hex value).

        assert_parse_fails(t, """\
            SyntaxError: Invalid escape sequence.

               |
             1 | "\\xF?"
               | ^

                from [test]:1:
        """,
        """\
            "\\xF?"
        """)

        # bad literal (empty Tuple)

        assert_parse_fails(t, """\
            SyntaxError: Cannot create an empty Tuple.
                from [test]:1:
        """,
        """\
            var v = <[]>
        """)
    }

    public define test_bad_operation
    {
        var t = Interpreter()

        # bad operation (String + String)

        assert_parse_fails(t, """\
            SyntaxError: Invalid operation: String + String.
                from [test]:6:
        """,
        """\
            class Example {
                public var @v = "test"
                public define tryit(other: Example) {
                    var combine =
                        "a" ++ other.v ++ "b" ++
                        "c" + @v ++ "d" ++ other.v ++ "e"
                }
            }

            var a = Example()
            a.tryit(a)
        """)

        # bad operation (== on different types)

        assert_parse_fails(t, """\
            SyntaxError: Invalid operation: List[Integer] == List[String].
                from [test]:4:
        """,
        """\
            var v1 = [1]
            var v2 = ["1"]

            v1 == v2
        """)

        # bad operation (!String)

        assert_parse_fails(t, """\
            SyntaxError: Invalid operation: !String.
                from [test]:1:
        """,
        """\
            var v = !"a"
        """)

        # bad operation (RuntimeError for infinite comparison)

        assert_parse_string(t, """
            enum Tree {
                Leaf(Integer),
                Branch(List[Tree])
            }

            var b0 = [Leaf(0)]
            var t0 = Branch(b0)

            b0[0] = t0

            var b1 = [Leaf(1)]
            var t1 = Branch(b1)

            b1[0] = t1

            try: {
                b1 == b0
                0 / 0
            except RuntimeError:
                b0[0] = Leaf(1)
                b1[0] = Leaf(1)
            }
        """)

        # Can't think of a good use case for math between these two.

        assert_parse_fails(t, """\
            SyntaxError: Invalid operation: Double + Byte.
                from [test]:1:
        """,
        """\
            var v = 1.0 + 'a'
        """)
    }

    public define test_bad_subscript
    {
        var t = Interpreter()

        # bad subscript (Tuple subscript out of range)

        assert_parse_fails(t, """\
            SyntaxError: Index 3 is out of range for Tuple[Integer, String].
                from [test]:1:
        """,
        """\
            var s: String = <[1, "2"]> [3]
        """)

        # bad subscript (Tuple subscript not a literal)

        assert_parse_fails(t, """\
            SyntaxError: Tuple subscripts must be Integer literals.
                from [test]:3:
        """,
        """\
            var a = 1
            var v = <[1, 2]>
            v[a] = 0
        """)

        # bad subscript (negative Tuple subscripts not allowed)

        assert_parse_fails(t, """\
            SyntaxError: Index -1 is out of range for Tuple[Integer].
                from [test]:2:
        """,
        """\
            var v = <[1]>
            v[-1] = 0
        """)

        # bad subscript (positive Tuple subscript out of range)

        assert_parse_fails(t, """\
            SyntaxError: Index 5 is out of range for Tuple[Integer].
                from [test]:2:
        """,
        """\
            var v = <[1]>
            v[5] = 1
        """)

        # bad subscript (invalid List subscript type)

        assert_parse_fails(t, """\
            SyntaxError: List subscripts require a simple numeric index.
            Index given: List[Integer]
                from [test]:2:
        """,
        """\
            var v = [1]
            v[v]
        """)

        # bad subscript (invalid ByteString subscript type)

        assert_parse_fails(t, """\
            SyntaxError: ByteString subscripts require a simple numeric index.
            Index given: Function (String => Integer)
                from [test]:2:
        """,
        """\
            var v = B"1"
            v[String.size]
        """)

        # bad subscript (invalid Hash subscript type)

        assert_parse_fails(t, """\
            SyntaxError: Hash index should be type 'Integer', not type 'String'.
                from [test]:1:
        """,
        """\
            [1 => 1, 2 => 2]["1"]
        """)

        # bad subscript (not supported on this type)

        assert_parse_fails(t, """\
            SyntaxError: Cannot subscript type 'Option[Integer]'.
                from [test]:1:
        """,
        """\
            Some(1)[1]
        """)

        # bad subscript (Bytestring subscript out of range)

        assert_parse_fails(t, """\
            IndexError: Subscript index -50 is out of range.
            Traceback:
                from [test]:1: in __main__
        """,
        """\
            B"Ā ā Ă ă Ą"[-50]
        """)

        # bad subscript (KeyError with large Integer value)

        assert_parse_fails(t, """\
            KeyError: 9000000000000000000
            Traceback:
                from [test]:2: in __main__
        """,
        """\
            var v = [1 => 1]
            var w = v[9000000000000000000]
        """)

        # bad subscript (KeyError with reasonable Integer value)

        assert_parse_fails(t, """\
            KeyError: 65535
            Traceback:
                from [test]:2: in __main__
        """,
        """\
            var v = [1 => 1]
            var w = v[65535]
        """)
    }

    public define test_broken_exit_fold
    {
        var t = Interpreter()

        # broken exit fold (with if)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.
                from [test]:10:
        """,
        """\
            define f(a: Integer) : Integer
            {
                if a == 1: {
                    return 1
                elif a == 1:
                    a = 2
                else:
                    return 3
                }
            }
        """)

        # broken exit fold (with enum match case)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.
                from [test]:9:
        """,
        """\
            define f(a: Option[Integer]) : Integer
            {
                match a: {
                    case Some(s):
                        s = 1
                    case None:
                        return 2
                }
            }
        """)

        # broken exit fold (with enum match final else)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.
                from [test]:9:
        """,
        """\
            define f(a: Option[Integer]) : Integer
            {
                match a: {
                    case Some(s):
                        s = 1
                    else:
                        return 2
                }
            }
        """)

        # broken exit fold (with class match case)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.
                from [test]:14:
        """,
        """\
            class One {}
            class Two < One {}
            class Three < Two {}

            define f(input: One): Integer {
                match input: {
                    case Three(t):
                        print(1)
                    case Two(t):
                        return 1
                    else:
                        return 2
                }
            }
        """)

        # broken exit fold (with class match final else)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.
                from [test]:14:
        """,
        """\
            class One {}
            class Two < One {}
            class Three < Two {}

            define f(input: One): Integer {
                match input: {
                    case Three(t):
                        return 0
                    case Two(t):
                        return 1
                    else:
                        print(1)
                }
            }
        """)

        # broken exit fold (with try base case)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.
                from [test]:8:
        """,
        """\
            define f(a: Integer) : Integer
            {
                try: {
                    a = 1
                except Exception:
                    return 2
                }
            }
        """)
    }

    public define test_code_after_exit
    {
        var t = Interpreter()

        # code after exit (after raise)

        assert_parse_fails(t, """\
            SyntaxError: Statement(s) after 'raise' will not execute.

               |
             2 | var v = 10
               | ^

                from [test]:2:
        """,
        """\
            raise ValueError("")
            var v = 10
        """)

        # code after exit (after value returned)

        assert_parse_fails(t, """\
            SyntaxError: Statement(s) after 'return' will not execute.

               |
             4 | var v = 2
               | ^

                from [test]:4:
        """,
        """\
            define f : Integer
            {
                return 1
                var v = 2
            }
        """)

        # code after exit (after Unit returned)

        assert_parse_fails(t, """\
            SyntaxError: Statement(s) after 'return' will not execute (no return type given).

               |
             1 | define test { return 0 }
               |                      ^

                from [test]:1:
        """,
        """\
            define test { return 0 }
        """)

        # code after exit (break)

        assert_parse_fails(t, """\
            SyntaxError: Statement(s) after 'break' will not execute.

               |
             3 | print(i)
               | ^

                from [test]:3:
        """,
        """\
            for i in 0...10: {
                break
                print(i)
            }
        """)

        # code after exit (continue)

        assert_parse_fails(t, """\
            SyntaxError: Statement(s) after 'continue' will not execute.

               |
             3 | print(i)
               | ^

                from [test]:3:
        """,
        """\
            for i in 0...10: {
                continue
                print(i)
            }
        """)
    }

    public define test_define_rules
    {
        var t = Interpreter()

        # define rules (verify returning a value)

        assert_parse_fails(t, """\
            SyntaxError: Missing return statement at end of function.

               |
             1 | define f : Integer {  }
               |                       ^

                from [test]:1:
        """,
        """\
            define f : Integer {  }
        """)

        # define rules (forbid scoop)

        assert_parse_fails(t, """\
            SyntaxError: Expected a class name here.

               |
             1 | define test: $1 {}
               |              ^

                from [test]:1:
        """,
        """\
            define test: $1 {}
        """)

        # define rules (no explicit Unit return)

        assert_parse_fails(t, """\
            SyntaxError: Unit return type does not need to be explicitly stated.

               |
             1 | define f: Unit {}
               |                ^

                from [test]:1:
        """,
        """\
            define f: Unit {}
        """)

        # define rules (redeclare parameter)

        assert_parse_fails(t, """\
            SyntaxError: a has already been declared.

               |
             1 | define f(a: Integer, a: Integer) {}
               |                      ^

                from [test]:1:
        """,
        """\
            define f(a: Integer, a: Integer) {}
        """)

        # define rules (within an if block)

        assert_parse_fails(t, """\
            SyntaxError: Cannot define a function here.

               |
             2 | define f {}
               | ^

                from [test]:2:
        """,
        """\
            if 1: {
                define f {}
            }
        """)
    }

    public define test_list_rules
    {
        var t = Interpreter()

        # list rules (ignore expected mismatch for first element)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'List[Integer]' to type 'List[String]'.
                from [test]:1:
        """,
        """\
            var v: List[String] = [1]
        """)
    }

    public define test_hash_rules
    {
        var t = Interpreter()

        # hash rules (invalid key provided in type)

        assert_parse_fails(t, """\
            SyntaxError: 'Option[Integer]' is not a valid key for Hash.

               |
             1 | var v: Hash[Option[Integer], String] = [Some(1) => "a"]
               |                                    ^

                from [test]:1:
        """,
        """\
            var v: Hash[Option[Integer], String] = [Some(1) => "a"]
        """)

        # hash rules (inferring invalid key)

        assert_parse_fails(t, """\
            SyntaxError: Type 'ByteString' is not a valid key for Hash.
                from [test]:1:
        """,
        """\
            var v = [B"" => 1]
        """)

        # hash rules (ignore expected mismatch for first key)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Hash[String, String]' to type 'Hash[Integer, String]'.
                from [test]:1:
        """,
        """\
            var v: Hash[Integer, String] = ["1" => "1"]
        """)

        # hash rules (ignore expected mismatch for first value)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Hash[Integer, Integer]' to type 'Hash[Integer, String]'.
                from [test]:1:
        """,
        """\
            var v: Hash[Integer, String] = [1 => 1]
        """)

        # hash rules (forbid invalid keys)

        assert_parse_fails(t, """\
            SyntaxError: Type 'A' is not a valid key for Hash.

               |
             1 | define f[A, B](a: A, b: B): Hash[A, B] { return [a => b] }
               |                                                  ^

                from [test]:1:
        """,
        """\
            define f[A, B](a: A, b: B): Hash[A, B] { return [a => b] }

            f(unit, unit)
        """)
    }

    public define test_incomplete_assign
    {
        var t = Interpreter()

        # incomplete assign (to global var)

        assert_parse_fails(t, """\
            SyntaxError: Right side of assignment is incomplete type 'Option[?]'.
                from [test]:1:
        """,
        """\
            var v = None
        """)

        # incomplete assign (to class property outside class)

        assert_parse_fails(t, """\
            SyntaxError: Right side of assignment is incomplete type 'Option[?]'.
                from [test]:6:
        """,
        """\
            class Test {
                public var @v = 0
            }

            var v = Test()
            v.v = None
        """)
    }

    public define test_not_assignable
    {
        var t = Interpreter()

        # not assignable (subscript result of a call)

        assert_parse_fails(t, """\
            SyntaxError: Left side of = is not assignable.
                from [test]:5:
        """,
        """\
            define f : List[Integer] {
                return [1]
            }

            f()[0] = 1
        """)

        # not assignable (parenth expr)

        assert_parse_fails(t, """\
            SyntaxError: Left side of = is not assignable.
                from [test]:3:
        """,
        """\
            var a = 1
            if 1: {
                (a + a) = 1
            }
        """)

        # not assignable (assign to class method by dot)

        assert_parse_fails(t, """\
            SyntaxError: Left side of = is not assignable.
                from [test]:5:
        """,
        """\
            class Example {
                public define f {}
            }

            Example.f = 10
        """)
    }

    public define test_var_rules
    {
        var t = Interpreter()

        # var rules (no self-initialization)

        assert_parse_fails(t, """\
            SyntaxError: Attempt to use uninitialized value 'v'.

               |
             1 | var v = v
               |         ^

                from [test]:1:
        """,
        """\
            var v = v
        """)

        # var rules (init expression required)

        assert_parse_fails(t, """\
            SyntaxError: Expected '=' <expression> here.
                from [test]:2:
        """,
        """\
            var v
        """)
    }

    public define test_verify_misc
    {
        var t = Interpreter()

        # verify misc (wrong type for return)

        assert_parse_fails(t, """\
            SyntaxError: return expected type 'Option[Integer]' but got type 'Integer'.
                from [test]:3:
        """,
        """\
            define f: Option[Integer]
            {
                return 1
            }
        """)

        # verify misc (wrong type for static Tuple)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Tuple[Integer]' to type 'Tuple[String]'.
                from [test]:1:
        """,
        """\
            var v: Tuple[String] = <[1]>
        """)

        # verify misc (invalid binary op)

        assert_parse_fails(t, """\
            SyntaxError: Invalid operation: String / Integer.
                from [test]:1:
        """,
        """\
            var v = ("abc" / 5)
        """)
    }

    public define test_ternary
    {
        var t = Interpreter()

        # ternary (? without value)

        assert_parse_fails(t, """\
            SyntaxError: Unexpected token within an expression.

               |
             1 | var v = (? 1 : 2)
               |          ^

                from [test]:1:
        """,
        """\
            var v = (? 1 : 2)
        """)

        # ternary (comma after ? before value)

        assert_parse_fails(t, """\
            SyntaxError: Expected a value, not ','.

               |
             1 | var v = (1 ? ,)
               |              ^

                from [test]:1:
        """,
        """\
            var v = (1 ? ,)
        """)

        # ternary (comma after ? after value)

        assert_parse_fails(t, """\
            SyntaxError: Comma not allowed within ternary.

               |
             1 | var v = (1 ? 2, )
               |               ^

                from [test]:1:
        """,
        """\
            var v = (1 ? 2, )
        """)

        # ternary (comma after colon before value)

        assert_parse_fails(t, """\
            SyntaxError: Expected a value, not ','.

               |
             1 | var v = (1 ? 2 : , )
               |                  ^

                from [test]:1:
        """,
        """\
            var v = (1 ? 2 : , )
        """)

        # ternary (comma instead of close parenth)

        assert_parse_fails(t, """\
            SyntaxError: Comma not allowed within ternary.

               |
             1 | var v = (1 ? 2 : 3,
               |                   ^

                from [test]:1:
        """,
        """\
            var v = (1 ? 2 : 3,
        """)

        # ternary (close parenth instead of colon)

        assert_parse_fails(t, """\
            SyntaxError: Expected closing token ':', not ')'.

               |
             1 | var v = (1 ? 2)
               |               ^

                from [test]:1:
        """,
        """\
            var v = (1 ? 2)
        """)

        # ternary (empty falsey part)

        assert_parse_fails(t, """\
            SyntaxError: Expected a value (var, literal, lambda, etc.) here.

               |
             1 | var v = (1 ? 2 : )
               |                  ^

                from [test]:1:
        """,
        """\
            var v = (1 ? 2 : )
        """)

        # ternary (wrong close token)

        assert_parse_fails(t, """\
            SyntaxError: Expected closing token ')', not ']'.

               |
             1 | var v = (1 ? 2 : 3]
               |                   ^

                from [test]:1:
        """,
        """\
            var v = (1 ? 2 : 3]
        """)

        # ternary (inconsistent class types)

        assert_parse_fails(t, """\
            SyntaxError: Ternary branches have different types:
            First: Integer
            Second: String
                from [test]:1:
        """,
        """\
            var v = (1 ? 2 : "3")
        """)
    }

    public define test_bad_for_range
    {
        # For loops are complex, so they get two testing functions.
        # This one's for `for x in y (by z)` loops.
        var t = Interpreter()

        # bad for range (require 'by' in for loop)

        assert_parse_fails(t, """\
            SyntaxError: Expected 'by' here.

               |
             1 | for i in 0...10 asdf
               |                 ^

                from [test]:1:
        """,
        """\
            for i in 0...10 asdf
        """)

        # bad for range (require 'in' within for loop)

        assert_parse_fails(t, """\
            SyntaxError: Expected 'in' here.

               |
             1 | for i asdf 0...10: {}
               |       ^

                from [test]:1:
        """,
        """\
            for i asdf 0...10: {}
        """)

        # bad for range (bad for step)

        assert_parse_fails(t, """\
            ValueError: for loop step cannot be 0.
            Traceback:
                from [test]:2: in __main__
        """,
        """\

            for i in 0...0 by 0: {
                0 / 0
            }
        """)

        # bad for range (assign in range expression)

        assert_parse_fails(t, """\
            SyntaxError: For range value expression contains an assignment.

               |
             2 | for i in v = 5...10: {
               |               ^

                from [test]:2:
        """,
        """\
            var v = 10
            for i in v = 5...10: {
                1
            }
        """)

        # bad for range (for range start using index)

        assert_parse_fails(t, """\
            SyntaxError: Attempt to use uninitialized value 'i'.

               |
             1 | for i in i...10: {
               |          ^

                from [test]:1:
        """,
        """\
            for i in i...10: {
            }
        """)

        # bad for range (for range end using index)

        assert_parse_fails(t, """\
            SyntaxError: Attempt to use uninitialized value 'i'.

               |
             1 | for i in 0...i: {
               |              ^

                from [test]:1:
        """,
        """\
            for i in 0...i: {
            }
        """)

        # bad for range (for range step using index)

        assert_parse_fails(t, """\
            SyntaxError: Attempt to use uninitialized value 'i'.

               |
             3 | for i in 0...10 by i: {
               |                    ^

                from [test]:3:
        """,
        """\
            var i = 0

            for i in 0...10 by i: {
            }
        """)
    }

    public define test_bad_for_list
    {
        # This one handles `for x(, y) in z` loops.
        var t = Interpreter()

        # Index/elem can't be upvalues either.

        assert_parse_fails(t, """\
            SyntaxError: Loop var cannot be an upvalue.

               |
             4 | for i in [1]: {}
               |     ^

                from [test]:4:
        """,
        """\
            define f {
                var i = 1
                define g {
                    for i in [1]: {}
                }
            }
        """)

        assert_parse_fails(t, """\
            SyntaxError: Loop var cannot be an upvalue.

               |
             4 | for i, elem in [1]: {}
               |        ^

                from [test]:4:
        """,
        """\
            define f {
                var elem = 1
                define g {
                    for i, elem in [1]: {}
                }
            }
        """)

        # Must have 1 or 2 vars.

        assert_parse_fails(t, """\
            SyntaxError: Expected 'in' here.

               |
             1 | for in [1]: {}
               |        ^

                from [test]:1:
        """,
        """\
            for in [1]: {}
        """)

        assert_parse_fails(t, """\
            SyntaxError: Expected 'in' here.

               |
             1 | for a, b, c in [1]: {}
               |         ^

                from [test]:1:
        """,
        """\
            for a, b, c in [1]: {}
        """)

        # No assignments

        assert_parse_fails(t, """\
            SyntaxError: For expression contains an assignment.

               |
             3 | for a, b in x = []: {}
               |                   ^

                from [test]:3:
        """,
        """\
            var x = [1]

            for a, b in x = []: {}
        """)

        # Type assurances

        assert_parse_fails(t, """\
            SyntaxError: For loop index must be Integer, but i has type String.

               |
             4 | for i, elem in [1]: {}
               |                   ^

                from [test]:4:
        """,
        """\
            define f {
                var i = ""

                for i, elem in [1]: {}
            }
        """)

        assert_parse_fails(t, """\
            SyntaxError: For list element type is not compatible:
            Expected (by elem): String
            Received: Integer

               |
             4 | for i, elem in [1]: {}
               |                   ^

                from [test]:4:
        """,
        """\
            define f {
                var elem = ""

                for i, elem in [1]: {}
            }
        """)

        assert_parse_fails(t, """\
            SyntaxError: For expression is not iterable:
            Expected: ByteString, List, or String
            Received: Option[Integer]

               |
             1 | for i, elem in Some(10): {}
               |                        ^

                from [test]:1:
        """,
        """\
            for i, elem in Some(10): {}
        """)

        assert_parse_fails(t, """\
            SyntaxError: For expression has incomplete type List[Option[?]].

               |
             1 | for i, elem in [None]: {}
               |                      ^

                from [test]:1:
        """,
        """\
            for i, elem in [None]: {}
        """)

        # No bidirectional unification on the element.

        assert_parse_fails(t, """\
            SyntaxError: For list element type is not compatible:
            Expected (by elem): Two
            Received: One

               |
             7 | for i, elem in [One()]: {}
               |                       ^

                from [test]:7:
        """,
        """\
            class One {}
            class Two < One {}

            define f {
                var elem = Two()

                for i, elem in [One()]: {}
            }
        """)
    }

    public define test_compound_assign_no_left_double_promotion
    {
        var t = Interpreter()

        # Math operations can promote either side Integer -> Double.
        # However, `an_int += some_double` is not a valid assignment.

        # Local var left side

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:4:
        """,
        """\
            define f {
                var v = 10

                v += 1.0
            }
        """)

        # Global var left side

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:3:
        """,
        """\
            var v = 10

            v += 1.0
        """)

        # Property left side

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:4:
        """,
        """\
            class Example {
                public var @v = 10

                @v += 1.0
            }
        """)

        # Dot access left side

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:7:
        """,
        """\
            class Example {
                public var @v = 10
            }

            var e = Example()

            e.v += 1.0
        """)

        # Upvalue left side

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:5:
        """,
        """\
            define f {
                var a = 10

                define g {
                    a += 1.0
                }
            }
        """)

        # Subscript left side

        assert_parse_fails(t, """\
            SyntaxError: Value 300 is out of range for a Byte.
                from [test]:2:
        """,
        """\
            var v = B"a"
            v[0] += 300
        """)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:2:
        """,
        """\
            var v = [1]
            v[0] += 1.0
        """)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:2:
        """,
        """\
            var v = [0 => 1]
            v[0] += 1.0
        """)

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Double' to type 'Integer'.
                from [test]:2:
        """,
        """\
            var v = <[0]>
            v[0] += 1.0
        """)
    }
}
