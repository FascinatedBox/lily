import (Interpreter,
        TestCase) "../t/testing"

class TestBadCall < TestCase
{
    public define test_basic_calls
    {
        var t = Interpreter()

        # basic calls (wrong count to plain call)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to f (1 for 0).
                from [test]:3:
        """,
        """\
            define f {}

            f(1)
        """)

        # basic calls (wrong count to global var function)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to v is invalid:
            Expected Type: String
            Received Type: Integer
                from [test]:2:
        """,
        """\
            var v = String.parse_i
            v(1)
        """)

        # basic calls (wrong count to parameter function)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to g (3 for 1).
                from [test]:2:
        """,
        """\
            define f(g: Function(Integer)) {
                g(1, 2, 3)
            }
        """)

        # basic calls (wrong count to anonymous function)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to (anonymous) (0 for 2).
                from [test]:9:
        """,
        """\
            class Test
            {
                public define test : Function(Integer, Integer => Boolean)
                {
                    return (|x, y| x == y)
                }
            }

            var z = Test().test()()
        """)

        # basic calls (empty Function as wrong type)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to f is invalid:
            Expected Type: Function ()
            Received Type: Integer
                from [test]:3:
        """,
        """\
            define f(g: Function()) {  }

            f(10)
        """)
    }

    public define test_class_calls
    {
        var t = Interpreter()

        # class calls (wrong count from method to ctor)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Test.method (1 for 2).
                from [test]:4:
        """,
        """\
            class Test
            {
                public define method(x: Integer) {
                    method()
                }
            }
        """)

        # class calls (wrong count to class method)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Test.method (2 for 1).
                from [test]:7:
        """,
        """\
            class Test
            {
                public define method {  }
            }

            var t = Test()
            t.method(123)
        """)

        # class calls (wrong count to class prop function)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to abc.a (3 for 0).
                from [test]:6:
        """,
        """\
            class abc {
                public var @a = (|| 10)
            }

            var d = abc()
            d.a(1, 2, 3)
        """)

        # class calls (wrong arg count to instance without instance method)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to String.ends_with (3 for 2).
                from [test]:1:
        """,
        """\
            String.ends_with("a", "b", "c")
        """)

        # class calls (wrong arg count to static method)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Example.g (2 for 3).
                from [test]:6:
        """,
        """\
            class Example {
                public static define g(a: Integer, b: String, c: Integer) {
                }
            }

            Example.g(10, "20")
        """)

        # class calls (wrong arg count to empty static method)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Time.now (1 for 0).
                from [test]:5:
        """,
        """\
            import (Time) time

            var t = Time.now()

            t.now()
        """)

        # class calls (wrong arg count to static call of static method)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to File.open is invalid:
            Expected Type: String
            Received Type: File
                from [test]:1:
        """,
        """\
            stdin.open("r")
        """)
    }

    public define test_closure_calls
    {
        var t = Interpreter()

        # closure calls (wrong count to closure function)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to g (1 for 0).
                from [test]:3:
        """,
        """\
            define f(a: Integer) {
                define g { a += 1 }
                g(1)
            }
        """)
    }

    public define test_enum_calls
    {
        var t = Interpreter()

        # enum calls (empty flat variant with args)

        assert_parse_fails(t, """\
            SyntaxError: Gamma is an empty variant that should not be called.
                from [test]:6:
        """,
        """\
            enum TestE {
                Alpha,
                Beta,
                Gamma
            }
            Gamma()
        """)

        # enum calls (non-empty flat variant no args)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Some (none for 1).
                from [test]:1:
        """,
        """\
            var v = Some
        """)

        # enum calls (non-empty flat variant too many args)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Some (2 for 1).
                from [test]:1:
        """,
        """\
            var v = Some(1, 2)
        """)

        # enum calls (non-empty scoped variant wrong count)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to One (2 for 1).
                from [test]:6:
        """,
        """\
            scoped enum Test[A] {
                One(A),
                Two
            }

            var opt: Test[Integer] = Test.One(1, 2)
        """)

        # enum calls (non-empty flat wrong vararg count)
        # This is blocked for consistency with defined functions, which are not
        # auto invoked when passed no args.

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to One (none for 0+).
                from [test]:6:
        """,
        """\
            enum Test[A] {
                One(A...),
                Two
            }

            var v = One
        """)
    }

    public define test_invalid_calls
    {
        var t = Interpreter()

        # invalid calls (call against Integer)

        assert_parse_fails(t, """\
            SyntaxError: Cannot anonymously call resulting type 'Integer'.
                from [test]:1:
        """,
        """\
            (10)()
        """)

        # invalid call (call against property holding Integer)

        assert_parse_fails(t, """\
            SyntaxError: Cannot anonymously call resulting type 'Integer'.
                from [test]:5:
        """,
        """\
            class Example {
                public var @a = 10
            }

            Example().a()
        """)

        # invalid call (call incomplete anonymous type)

        assert_parse_fails(t, """\
            SyntaxError: Cannot call Example.y using incomplete type Example[Function (?, ? => List[?])].
                from [test]:10:
        """,
        """\
            define f[A, B](a: A, b: A): List[A] { return [a, b] }

            class Example[A](a: A) {
                public var @x = a
                public define y: A {
                    return @x
                }
            }

            var v = Example(f).y()(1, 2).push("3")

            print(v[0].size())
        """)

        # invalid call of incomplete result

        assert_parse_fails(t, """\
            SyntaxError: Cannot call an incomplete type (?).
                from [test]:3:
        """,
        """\
            define f[A](a: List[A]): A { return a[0] }

            f([])()
        """)
    }

    public define test_keyarg_calls
    {
        var t = Interpreter()

        # keyarg calls (too many args)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to f (4 for 1).
                from [test]:2:
        """,
        """\
            define f(:a a: Integer) {}
            f(1, 2, 3, :a 1)
        """)

        # keyarg calls (not there)

        assert_parse_fails(t, """\
            SyntaxError: f does not have a keyword named ':b'.
                from [test]:3:
        """,
        """\
            define f(:a a: Integer) {  }

            f(:b 1)
        """)

        # keyarg calls (missing keyargs before and after)

        assert_parse_fails(t, """\
            SyntaxError: Call to f is missing parameters:
            * Parameter #1 of type Integer.
            * Parameter #3 of type Double.
                from [test]:3:
        """,
        """\
            define f(a: Integer, :b b: String, c: Double) {}

            f(:b "a")
        """)

        # keyarg calls (duplicate target by keyargs only)

        assert_parse_fails(t, """\
            SyntaxError: Call to f has multiple values for parameter ':a'.
                from [test]:2:
        """,
        """\
            define f(:a a: Integer, :b b: Integer) {}
            f(:a 1, :a 2)
        """)

        # keyarg calls (wrong type)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to f is invalid:
            Expected Type: Integer
            Received Type: String
                from [test]:2:
        """,
        """\
            define f(:a a: Integer) {}
            f(:a "")
        """)

        # keyarg calls (valid first arg)

        assert_parse_fails(t, """\
            SyntaxError: Call to f is missing parameters:
            * Parameter #2 (:b) of type String.
                from [test]:3:
        """,
        """\
            define f(:a a: Integer, :b b: String) {}

            f(:a "1")
        """)

        # keyarg calls (missing parameter out of order)

        assert_parse_fails(t, """\
            SyntaxError: Call to check is missing parameters:
            * Parameter #2 (:two) of type Integer.
                from [test]:10:
        """,
        """\
            define check(
                :one   one:   Integer,
                :two   two:   Integer,
                :three three: Integer,
                :four  four:  Integer,
                :five  five:  Integer)
            {
            }

            check(
                :three 3,
                :five  5,
                :one   1,
                :four  4
            )
        """)
    }

    public define test_mixarg_calls
    {
        var t = Interpreter()

        # mixarg calls (opt+var with too few)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to f (1 for 2+).
                from [test]:5:
        """,
        """\
            define f(a: Integer, b: Integer, c: *Integer = 10, d: *Integer...=[]) {

            }

            f(1)
        """)

        # mixarg calls (key+var missing only parameters)

        t = Interpreter()
        assert_parse_fails(t, """\
            SyntaxError: Call to f is missing parameters:
            * Parameter #1 (:a) of type Integer.
            * Parameter #3 (:c) of type Double.
                from [test]:3:
        """,
        """\
            define f(:a a: Integer, :b b: String, :c c: Double, :d d: Byte...) {}

            f(:b "a")
        """)

        # mixarg calls (key+opt missing only parameters)

        t = Interpreter()
        assert_parse_fails(t, """\
            SyntaxError: Call to f is missing parameters:
            * Parameter #1 (:a) of type Integer.
                from [test]:6:
        """,
        """\
            define f(:a a: Integer,
                     :b b: String,
                     :c c: *Double=0.0,
                     :d d: *Boolean=true) {}

            f(:b "a")
        """)
    }

    public define test_optarg_calls
    {
        var t = Interpreter()

        # optarg calls (too few with 1 required)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to f (0 for 1..2).
                from [test]:5:
        """,
        """\
            define f(a: Integer, b: *Integer = 10) {

            }

            f()
        """)
    }

    public define test_vararg_calls
    {
        var t = Interpreter()

        # vararg calls (wrong count when using class method)

        assert_parse_fails(t, """\
            SyntaxError: Wrong number of arguments to Test.method (1 for 2+).
                from [test]:7:
        """,
        """\
            class Test
            {
                public define method(x: Integer, y: Integer...) {  }
            }

            var t = Test()
            t.method()
        """)

        # vararg calls (wrong type BEFORE the end)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to f is invalid:
            Expected Type: Integer
            Received Type: String
                from [test]:4:
        """,
        """\
            define f(a: Integer, b: String...) {
            }

            f("4")
        """)

        # vararg calls (wrong type AT the end)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to f is invalid:
            Expected Type: Integer
            Received Type: String
                from [test]:4:
        """,
        """\
            define f(a: Integer...) {
            }

            f("4")
        """)

        # vararg calls (wrong type PAST the end)

        assert_parse_fails(t, """\
            SyntaxError: Argument #4 to f is invalid:
            Expected Type: Integer
            Received Type: String
                from [test]:4:
        """,
        """\
            define f(a: Integer...) {
            }

            f(1, 2, 3, "4")
        """)
    }

    public define test_vararg_transfer
    {
        var t = Interpreter()

        # arg before transfer

        assert_parse_fails(t, """\
            SyntaxError: Vararg transfer (*args) must be the only excess arg in a vararg call.
                from [test]:3:
        """,
        """\
            define f(a: Integer...) {}
            var v = [1]
            f(1, *v)
        """)

        # arg after transfer

        assert_parse_fails(t, """\
            SyntaxError: Cannot have arguments after vararg transfer (*args).
                from [test]:3:
        """,
        """\
            define f(a: Integer...) {}
            var v = [1]
            f(*v, "a")
        """)

        # key+transfer is not implemented

        assert_parse_fails(t, """\
            SyntaxError: Vararg transfer (*args) must be the only excess arg in a vararg call.
                from [test]:2:
        """,
        """\
            define f(:_ a: Integer...) {}
            f(:a *[1])
        """)

        # key+transfer is not implemented

        assert_parse_fails(t, """\
            SyntaxError: Vararg transfer (*args) must be the only excess arg in a vararg call.
                from [test]:2:
        """,
        """\
            define f(:_ a: Integer...) {}
            f(:a *[1])
        """)

        # blocked at toplevel

        assert_parse_fails(t, """\
            SyntaxError: Vararg transfer (*args) must be the only excess arg in a vararg call.
                from [test]:1:
        """,
        """\
            var v = *[1]
        """)

        # wrong type (unsolved)

        assert_parse_fails(t, """\
            SyntaxError: Mismatched type for vararg transfer (*args) to f:
            Expected: List[String]
            Received: List[Integer]
                from [test]:2:
        """,
        """\
            define f(a: String...) {}
            f(*[1])
        """)

        # wrong type (solved)

        assert_parse_fails(t, """\
            SyntaxError: Mismatched type for vararg transfer (*args) to f:
            Expected: List[Byte]
            Received: List[List[Integer]]
                from [test]:2:
        """,
        """\
            define f[A](a: A, b: A...) {}
            f('1', *[[1]])
        """)

        # wrong type (unsolved with call pipe)

        assert_parse_fails(t, """\
            SyntaxError: Mismatched type for vararg transfer (*args) to f:
            Expected: List[String]
            Received: List[Integer]
                from [test]:2:
        """,
        """\
            define f(a: String...) {}
            *[1] |> f
        """)
    }

    public define test_call_incomplete_type
    {
        var t = Interpreter()

        assert_parse_fails(t, """\
            SyntaxError: Cannot call Example.pushcall using incomplete type Example[?].
                from [test]:14:
        """,
        """\
            class Example[A] {
                public var @x: List[A] = []

                public define pushcall(a: A, fn: Function(A)): self
                {
                    @x.push(a)

                    for x in @x: {
                        fn(x)
                    }
                }
            }

            Example().pushcall(1, Integer.to_s)
                     .pushcall("1", String.parse_i)
        """)
    }
}
