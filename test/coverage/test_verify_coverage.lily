import (Interpreter,
        LibraryTarget,
        StringTarget,
        TestCase) "../t/testing"
import "../t/backbone"

class TestVerifyCoverage < TestCase
{
    private var @t_covlib =
        LibraryTarget(
            :path "covlib",
            :redirect "test/t/backbone"
        )

    private var @t_packagelib =
        LibraryTarget(
            :path "packagelib",
            :redirect "test/t/backbone"
        )

    public define test_api_misc
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            covlib.cover_list_reserve()
            if covlib.cover_func_check((|x| 10 ), Integer.to_s) == false: {
                raise Exception("cover_func_check failed.")
            }
            covlib.cover_list_sfs()

            class Example < Exception("") {

            }

            if covlib.cover_id_checks(unit, covlib.Container(""), "abc") == false: {
                raise Exception("cover_id_checks failed.")
            }

            define f2(a: Integer) {}

            if covlib.cover_push_boolean() != true: {
                raise Exception("cover_push_boolean failed.")
            }

            covlib.cover_value_as('a',
                                  B"123",
                                  Exception("test"),
                                  1.0,
                                  stdout,
                                  f2,
                                  covlib.Foreign(),
                                  [1 => 1],
                                  1,
                                  "1")

            if covlib.cover_value_group(true,
                                        '1',
                                        B"1",
                                        1.0,
                                        None,
                                        stdout,
                                        f2,
                                        [1 => 1],
                                        covlib.Foreign(),
                                        Exception("x"),
                                        1,
                                        [1],
                                        "1",
                                        <[1]>,
                                        unit,
                                        Some(1)) == false: {
                raise Exception("cover_value_group failed.")
            }

            if covlib.cover_function_bytecode(String.size, f2) == false: {
                0 / 0
            }

            define fn(a: Function()) {
                # This covers calling a foreign function from a register.
                a()
            }

            fn(covlib.cover_misc_api)

            # The api contains functions to raise common errors.
            # These two are covered here, because the prelude does not use them.
            var ok = false

            try: {
                covlib.raise_dbzerror()
            except DivisionByZeroError:
                ok = true
            }

            if ok == false: {
                0 / 0
            }

            ok = false

            try: {
                covlib.raise_keyerror()
            except KeyError:
                ok = true
            }

            if ok == false: {
                0 / 0
            }

            # Verify 'self' return type.
            if covlib.C2("asdf", 45).nothing().check() != 45: {
                0 / 0
            }
        """)
    }

    public define test_api_optional
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib as c

            var b_values = [
                <["A, B, C", 0, c.cover_optional_boolean(false, false, false)]>,
                <["A, B, _", 1, c.cover_optional_boolean(false, false)]>,
                <["A, _, C", 1, c.cover_optional_boolean(false, :c false)]>,
                <["A, _, _", 2, c.cover_optional_boolean(false)]>,
                <["_, B, C", 1, c.cover_optional_boolean(:b false, :c false)]>,
                <["_, B, _", 2, c.cover_optional_boolean(:b false)]>,
                <["_, _, C", 2, c.cover_optional_boolean(:c false)]>,
                <["_, _, _", 3, c.cover_optional_boolean()]>,
            ]

            var d_values = [
                <["A, B, C", 6.0, c.cover_optional_double(1.0, 2.0, 3.0)]>,
                <["A, B, _", 303.0, c.cover_optional_double(1.0, 2.0)]>,
                <["A, _, C", 204.0, c.cover_optional_double(1.0, :c 3.0)]>,
                <["A, _, _", 501.0, c.cover_optional_double(1.0)]>,
                <["_, B, C", 105.0, c.cover_optional_double(:b 2.0, :c 3.0)]>,
                <["_, B, _", 402.0, c.cover_optional_double(:b 2.0)]>,
                <["_, _, C", 303.0, c.cover_optional_double(:c 3.0)]>,
                <["_, _, _", 600.0, c.cover_optional_double()]>,
            ]

            var i_values = [
                <["A, B, C", 6, c.cover_optional_integer(1, 2, 3)]>,
                <["A, B, _", 303, c.cover_optional_integer(1, 2)]>,
                <["A, _, C", 204, c.cover_optional_integer(1, :c 3)]>,
                <["A, _, _", 501, c.cover_optional_integer(1)]>,
                <["_, B, C", 105, c.cover_optional_integer(:b 2, :c 3)]>,
                <["_, B, _", 402, c.cover_optional_integer(:b 2)]>,
                <["_, _, C", 303, c.cover_optional_integer(:c 3)]>,
                <["_, _, _", 600, c.cover_optional_integer()]>,
            ]

            var s_values = [
                <["A, B, C", "123", c.cover_optional_string("1", "2", "3")]>,
                <["A, B, _", "12",  c.cover_optional_string("1", "2")]>,
                <["A, _, C", "13",  c.cover_optional_string("1", :c "3")]>,
                <["A, _, _", "1",   c.cover_optional_string("1")]>,
                <["_, B, C", "23",  c.cover_optional_string(:b "2", :c "3")]>,
                <["_, B, _", "2",   c.cover_optional_string(:b "2")]>,
                <["_, _, C", "3",   c.cover_optional_string(:c "3")]>,
                <["_, _, _", "",    c.cover_optional_string()]>,
            ]

            define check_table[A, B](source: List[Tuple[A, B, B]]) {
                for i in 0...source.size() - 1: {
                    var v = source[i]

                    if v[1] != v[2]: {
                        raise Exception("Test '{0}' failed (want {1}, have {2})."
                                .format(v[0], v[1], v[2]))
                    }
                }
            }

            check_table(b_values)
            check_table(d_values)
            check_table(i_values)
            check_table(s_values)
        """)

        t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib as c

            var v = c.cover_optional_integer
                        |> c.cover_optional_keyarg_call

            if v != 401: {
                raise Exception("Keyopt failed.")
            }
        """)
    }

    public define test_file_error_sticking
    {
        var t = Interpreter()

        t.parse_string("[ctx]", "var v = 10")
        t.parse_file("potato")
        t.parse_string("[ctx]", "var w = v + 10")

        assert_equal(t.error(), "")
    }

    public define test_gc_marking_enums
    {
        var t = Interpreter()

        # This leaks if Branch is not tagged by the gc.
        assert_parse_string(t, """
            enum Tree {
                Leaf,
                Branch(List[Tree])
            }

            var a = Leaf
            var b = [a]
            var c = Branch(b)

            b[0] = c
        """)
    }

    public define test_foreign_generic
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            define f(a: covlib.ForeignGeneric[Integer, String]) {}
        """)
    }

    public define test_foreign_enums
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        # foreign enums (flat)

        assert_parse_string(t, """
            import (FlatEnum, FlatOne, FlatTwo, FlatThree,
                    make_flat_n) covlib

            var v = "" ++ FlatTwo
            if v != "FlatTwo": {
                raise Exception("Failed to interpolate.")
            }

            define f(l: List[FlatEnum]): List[Integer] {
                var result: List[Integer] = []

                for i in 0...l.size() - 1: {
                    var e = l[i]
                    match e: {
                        case FlatOne:
                            result.push(1)
                        case FlatTwo:
                            result.push(2)
                        case FlatThree:
                            result.push(3)
                    }
                }

                return result
            }

            var v2 = f([FlatOne, FlatTwo, FlatThree, FlatTwo, FlatOne])
            if v2 != [1, 2, 3, 2, 1]: {
                raise Exception("Failed to match and transform.")
            }

            var v3 = [
                make_flat_n(1) ++ "",
                make_flat_n(2) ++ "",
                make_flat_n(3) ++ "",
            ]

            if v3 != ["FlatOne",
                      "FlatTwo",
                      "FlatThree"]: {
                raise Exception("Failed to interpolate foreign made variants.")
            }
        """)

        # foreign enums (scoped)

        t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import (FlatEnum, FlatOne, FlatTwo, FlatThree,
                    make_scoped_n) covlib

            var v = "" ++ FlatTwo
            if v != "FlatTwo": {
                raise Exception("Failed to interpolate.")
            }

            define f(l: List[FlatEnum]): List[Integer] {
                var result: List[Integer] = []

                for i in 0...l.size() - 1: {
                    var e = l[i]
                    match e: {
                        case FlatOne:
                            result.push(1)
                        case FlatTwo:
                            result.push(2)
                        case FlatThree:
                            result.push(3)
                    }
                }

                return result
            }

            var v2 = f([FlatOne, FlatTwo, FlatThree, FlatTwo, FlatOne])
            if v2 != [1, 2, 3, 2, 1]: {
                raise Exception("Failed to match and transform.")
            }

            var v3 = [
                make_scoped_n(1) ++ "",
                make_scoped_n(2) ++ "",
                make_scoped_n(3) ++ "",
            ]

            if v3 != ["ScopedEnum.ScopedOne",
                      "ScopedEnum.ScopedTwo",
                      "ScopedEnum.ScopedThree"]: {
                raise Exception("Failed to interpolate foreign made variants.")
            }

        """)
    }

    public define test_foreign_error_recovery
    {
        var t = Interpreter()

        assert_parse_string(t, """
            var h = [1 => 1, 2 => 2, 3 => 3, 4 => 4]

            try: {
                h.select(|key, value| h.delete(key) true )
                raise Exception("Hash.delete did not fail properly during select.")
            except RuntimeError:
                0
            }

            try: {
                h.delete(4)
            except RuntimeError:
                raise Exception("Hash.delete should not fail outside select.")
            }

            try: {
                h.select(|key, value|
                    if key == 2: {
                        key = key / 0
                    }

                    true )
                raise Exception("Hash.select should have raised an error.")
            except DivisionByZeroError:
                0
            }

            try: {
                h.delete(1)
            except DivisionByZeroError:
                raise Exception("Hash.select did not recover properly.")
            }
        """)
    }

    public define test_foreign_inherit
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            class InheritedContainer(x: String) < covlib.Container(x) {
                public define do_update(y: String) {
                    update(y ++ y)
                }
            }

            var i = InheritedContainer("abc")
            i.do_update("xyz")

            if i.fetch() != "xyzxyz": {
                raise Exception("failed")
            }
        """)
    }

    public define test_hash_corruption
    {
        var t = Interpreter()

        assert_parse_string(t, """
            var hash: Hash[String, Integer] = []

            define f: Integer {
                var i = 1
                hash[i ++ i] = i

                return hash["11"]
            }

            f()
            f()
        """)
    }

    public define test_long_bytestring
    {
        var t = Interpreter()

        assert_parse_string(t, """
            # This covers internal Bytestring caching.
            var a = B"123"
            var b = B"12345678901234567890123456789012345678901234567890"
            var c = B"12345678901234567890123456789012345678901234567890"
        """)
    }

    public define test_parse_expr_api
    {
        var t = Interpreter()

        # parse expr api (simple Integer value)

        assert_equal(
            t.parse_expr("[test]", "10").unwrap(),
            "10\n"
        )

        # parse expr api (String values are quoted)

        assert_equal(
            t.parse_expr("[test]", "\"asdf\"").unwrap(),
            "\"asdf\"\n"
        )

        assert_equal(
            t.parse_expr("[test]", "unit").unwrap(),
            ""
        )

        # parse expr api (broken expression returns None, sets error)
        # Since this doesn't go through assert_*_fails, it gets the raw string.
        # In this case, forget about the indentation and match exactly.

        t.parse_expr("[test]", "10 +").is_none() |> assert_true

        var expect = """\
SyntaxError: Expected a value (var, literal, lambda, etc.) here.

   |
 1 | 10 +
   |     ^

    from [test]:1:
"""

        assert_equal(t.error(), expect)

        # Subsequent calls should yield the same message.
        assert_equal(t.error(), expect)
    }

    public define test_unit_call_error
    {
        var t = Interpreter()

        assert_parse_fails(t, """\
            SyntaxError: Class Unit does not have a constructor.

               |
             1 | var v = Unit()
               |             ^

                from [test]:1:
        """,
        """\
            var v = Unit()
        """)
    }

    public define test_print_main_type
    {
        var t = Interpreter()

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Integer' to type 'Function ()'.

               |
             2 | v = 1
               |   ^

                from [test]:2:
        """,
        """\
            var v = __main__
            v = 1
        """)
    }

    public define test_print_varopt_type
    {
        var t = Interpreter()

        assert_parse_fails(t, """\
            SyntaxError: Cannot assign type 'Function (*Integer...)' to type 'Integer'.

               |
             3 | var v: Integer = f
               |                ^

                from [test]:3:
        """,
        """\
            define f(a: *Integer... = []) { }

            var v: Integer = f
        """)
    }

    public define test_scoop_narrowing
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        # scoop narrowing (base tests with and without args)

        assert_parse_string(t, """
            import covlib

            define f {}
            define g(a: String): Integer { return 0 }
            define h(a: Integer, b: String): Boolean { return true }
            define i(a: Integer, b: String, c: Double): Boolean { return true }

            covlib.scoop_narrow(f)
            covlib.scoop_narrow(g)
            covlib.scoop_narrow(h)

            covlib.scoop_narrow_with_args(h)
            covlib.scoop_narrow_with_args(i)
        """)

        # scoop narrowing (wrong result type skip scoop)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to scoop_narrow_with_args is invalid:
            Expected Type: Function (Integer, String, $1 => Boolean)
            Received Type: Function (Integer, String => Integer)

               |
             3 | covlib.scoop_narrow_with_args(j)
               |                               ^

                from [test]:3:
        """,
        """\
            define j(a: Integer, b: String): Integer { return 0 }

            covlib.scoop_narrow_with_args(j)
        """)

        # scoop narrowing (wrong result type with scoop)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to scoop_narrow_with_args is invalid:
            Expected Type: Function (Integer, String, $1 => Boolean)
            Received Type: Function (Integer, String, Double => Integer)

               |
             3 | covlib.scoop_narrow_with_args(j)
               |                               ^

                from [test]:3:
        """,
        """\
            define j(a: Integer, b: String, c: Double): Integer { return 0 }

            covlib.scoop_narrow_with_args(j)
        """)

        # scoop narrowing (too few before scoop)

        assert_parse_fails(t, """\
            SyntaxError: Argument #1 to scoop_narrow_with_args is invalid:
            Expected Type: Function (Integer, String, $1 => Boolean)
            Received Type: Function (Integer => Boolean)

               |
             3 | covlib.scoop_narrow_with_args(j)
               |                               ^

                from [test]:3:
        """,
        """\
            define j(a: Integer): Boolean { return true }

            covlib.scoop_narrow_with_args(j)
        """)
    }

    public define test_library_exporting_modules
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            if covlib.farm.carrot_count != 100: {
                0 / 0
            }
        """)
    }

    public define test_library_exporting_constants
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            var v = 10

            if covlib.farm.a != 10 ||
               covlib.farm.b != 5.5 ||
               covlib.farm.c != "test" ||
               v != 10: {
                0 / 0
            }
        """)
    }

    public define test_assign_optimize
    {
        var t = Interpreter()

        # assign optimize (typecast does not crash)

        assert_parse_string(t, """
            define f(a: Integer) { var b = a.@(Integer) }
        """)

        # assign optimize (self does not crash)

        assert_parse_string(t, """
            class Example {
                public define g: Example {
                    var a = self
                    return a
                }
            }

            var e = Example()
            var b = e.g() ++ e
        """)
    }

    public define test_internal_sandbox
    {
        var t = backbone.Interpreter.new_sandboxed()

        t.open_math_library()

        # internal sandbox (predefined modules not automatically loaded)
        # The traceback also shows that only native files are looked at.

        assert_parse_fails(t, """\
            SyntaxError: Cannot import 'sys':
                no builtin module 'sys'
                no file '.\/sys.lily'
                no file '.\/packages\/sys\/src\/sys.lily'

               |
             1 | import sys
               |        ^

                from [test]:1:
        """,
        """\
            import sys
        """)

        # internal sandbox (math library available when opened)

        assert_parse_string(t, """
            import math

            if math.abs(-1) != 1: {
                0/0
            }
        """)

        # internal sandbox (opening files blocked)

        assert_parse_fails(t, """\
            RuntimeError: Not allowed to open files in sandbox mode.
            Traceback:
                from [prelude]: in File.open
                from [test]:28: in __main__
        """,
        """\
            try: {
                File.open("test.txt", "w")
                0/0
            except RuntimeError:
            }

            # Use paths that don't exist to make sure IOError doesn't activate
            # before RuntimeError.

            try: {
                File.open("doesnotexist.txt", "r")
                0/0
            except RuntimeError:
            }

            try: {
                File.write_to_path("doesnotexist.txt", "asdf")
                0/0
            except RuntimeError:
            }

            try: {
                File.read_to_string("doesnotexist.txt")
                0/0
            except RuntimeError:
            }

            File.open("test.txt", "a")
        """)
    }

    public define test_bytestring_literal_no_mutation
    {
        var t = Interpreter()

        # ByteString subscript assignment should not mutate literals.

        assert_parse_string(t, """
            define f: ByteString {
                var v = B"abc"

                v[0] = 'c'
                return v
            }

            define g: ByteString {
                var w = B"abc"

                return w
            }

            if f() == g(): {
                0 / 0
            }
        """)
    }

    public define test_direct_import_keyarg_and_another
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        # The first item dynaloads keyword arguments.
        assert_parse_string(t, """
            import (optcount, cover_misc_api) covlib
        """)
    }

    public define test_value_variant_foreign_method
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import (ScopedValueDirection) covlib

            var v: ScopedValueDirection = .North
            var in: List[ScopedValueDirection] = [.North, .East, .South, .West]
            var out: List[ScopedValueDirection] = [.East, .South, .West, .North]

            in = in.map(ScopedValueDirection.turn_right)

            if in != out: {
                0/0
            }
        """)
    }

    public define test_coroutine_closure_does_not_leak
    {
        # Set the gc start low so the gc invokes frequently.
        # This relies on the gc firing at the right time.
        var t = backbone.Interpreter.new_with_gc(1)

        # Running this under valgrind will show a leak.
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define fn: Coroutine[Integer, Unit] {
                define fn2(co: Coroutine[Integer, Unit]): Integer { return 0 }

                return Coroutine.build(fn2)
            }

            for i in 0...2: {
                fn()
            }
        """)
    }

    public define test_gc_on_variants_with_gc_values
    {
        # Low gc so it crashes if incorrect.
        var t = backbone.Interpreter.new_with_gc(1)

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define function(coroutine: Coroutine[Unit, Unit]) {
                coroutine.yield(unit)
            }

            class Object {
                private var @coroutine = Some(Coroutine.build(function))

                public define step {
                    with @coroutine as Some(coroutine): {
                        coroutine.resume()
                        if !coroutine.is_waiting(): {
                            @coroutine = None
                        }
                    }
                }
            }

            var objects: List[Object] = [Object(), Object()]
            objects.map(Object.step)
        """)
    }

    public define test_call_prep_register_growth
    {
        # This was originally showing a warning in valgrind.
        var t = Interpreter()

        assert_parse_string(t, """
            var a = 0, b = 0, c = 0, d = 0, e = 0,
                f = 0, g = 0, h = 0, i = 0, j = 0,
                k = 0, l = 0, m = 0, n = 0, o = 0,
                p = 0
        """)
    }

    public define test_nested_foreign_variants
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            var w = covlib.warm.Color.Yellow
            var c = covlib.cool.Color.Green

            var c1 = covlib.Color.Warm(w)
            var c2 = covlib.Color.Cool(c)

            var ce = covlib.CoolExample()

            if ce.cool_to_i(c) != 1: { 0/0 }
            if covlib.warm_to_i(w) != 1: { 0/0 }
        """)
    }

    public define test_ec_error_message
    {
        var t = Interpreter.with_targets(
            @t_covlib
        )

        assert_parse_string(t, """
            import covlib

            define h(a: List[Integer]): Integer { return a[5] }
            define g { h([1]) }
            define f { g() }
            define k {}

            define fn
            {
                var v = 10
                var w = "2"

                try: {
                    covlib.ec_error_passthru(f)
                except IndexError as e:
                    # Make sure the exception was constructed properly.
                    if e.message == "" ||
                       e.traceback.size() != 6: {
                        0/0
                    }
                }

                # Use local vars to test stack restoration.

                if v + w.parse_i().unwrap() != 12: {
                    0/0
                }

                covlib.ec_error_passthru(k)

                if v + w.parse_i().unwrap() != 12: {
                    0/0
                }
            }

            fn()
        """)
    }
}
