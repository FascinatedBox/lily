import (Interpreter,
        TestCase) "../t/testing"

class TestPkgCoroutine < TestCase
{
    public define test_basic
    {
        var t = Interpreter()

        # basic (resume once)

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define f(co: Coroutine[Integer, Unit]): Integer {
                co.yield(1)
                return 0
            }

            if Coroutine.build(f).resume().unwrap() != 1: {
               raise Exception("Failed.")
            }
        """)

        # basic (resume twice)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define f(co: Coroutine[Integer, Unit]) {
                co.yield(1)
                co.yield(2)
            }

            var c = Coroutine.build(f)

            if c.resume().unwrap() != 1 ||
               c.resume().unwrap() != 2: {
               raise Exception("Failed.")
            }
        """)

        # basic (yield from a List of values)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define f(co: Coroutine[Integer, Unit], source: List[Integer]): Integer {
                for i in 0...source.size() - 1: {
                    co.yield(source[i])
                }

                return 0 / 0
            }

            var input = [5, 10, 15, 20, 25]
            var co = Coroutine.build_with_value(f, input)
            var output = input.map(|l| co.resume().unwrap() )

            if input != output || co.resume().is_some(): {
               raise Exception("Failed.")
            }
        """)

        # basic (check call stack)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define fn(co: Coroutine[List[String], Unit]) {
                co.yield(calltrace())
            }

            var expect = ["[test]:5: in fn"]

            if Coroutine.build(fn).resume().unwrap() != expect: {
                raise Exception("Failed.")
            }
        """)
    }

    public define test_co_receive
    {
        var t = Interpreter()

        # receive (Integer value transfer)

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define f(co: Coroutine[Integer, Integer]): Integer {
                for i in 0...5: {
                    co.yield(co.receive() * 2)
                }

                return 0 / 0
            }

            var co = Coroutine.build(f)
            var lst: List[Integer] = []

            for i in 1...5: {
                co.resume_with(i).unwrap() |> lst.push
            }

            if lst != [2, 4, 6, 8, 10]: {
                raise Exception("Failed.")
            }
        """)
    }

    public define test_coroutine_closure
    {
        var t = Interpreter()

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define producer(yield_over: List[Integer]): Coroutine[Integer, Unit] {
                define producer_fn(co: Coroutine[Integer, Unit]): Integer {
                    var index = 0
                    for i in 0...yield_over.size() - 1: {
                        co.yield(yield_over[i])
                    }

                    return 0 / 0
                }

                return Coroutine.build(producer_fn)
            }

            define filter(incoming_co: Coroutine[Integer, Unit]): Coroutine[Integer, Unit] {
                define filter_fn(filter_co: Coroutine[Integer, Unit]): Integer {
                    while 1: {
                        var x = incoming_co.resume().unwrap()

                        if x % 2: {
                            continue
                        }

                        filter_co.yield(x)
                    }

                    return 0 / 0
                }

                return Coroutine.build(filter_fn)
            }

            define coro_all(co: Coroutine[Integer, Unit]): List[Integer] {
                var result: List[Integer] = []

                while 1: {
                    match co.resume(): {
                        case Some(s):
                            result.push(s)
                        case None:
                            break
                    }
                }

                return result
            }

            var co_producer = producer([1, 2, 3, 4, 5, 6])
            var co_filter = filter(co_producer)
            var output = coro_all(co_filter)

            if output != [2, 4, 6]: {
                raise Exception("Failed.")
            }
        """)
    }

    public define test_coroutine_exception
    {
        var t = Interpreter()

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define f(co: Coroutine[Integer, Unit]): Integer {
                co.yield(1 / 0)
                return 0
            }

            if Coroutine.build(f).resume().is_some(): {
               raise Exception("Failed.")
            }
        """)
    }

    public define test_incorrect
    {
        var t = Interpreter()

        # incorrect (close over a foreign function)

        assert_parse_fails(t, """\
            RuntimeError: Only native functions can be coroutines.
            Traceback:
                from [coroutine]: in Coroutine.build
                from [test]:3: in __main__
        """,
        """\
            import (Coroutine) coroutine

            Coroutine.build(Coroutine.receive)
        """)

        # incorrect (resume broken coroutine)

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define broken(co: Coroutine[Integer, Unit]): Integer {
                return 1 / 0
            }

            var co = Coroutine.build(broken)
            var lst = [co.resume(), co.resume()]

            if lst != [None, None]: {
               raise Exception("Failed.")
            }
        """)

        # incorrect (resume done coroutine)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define done(co: Coroutine[Integer, Unit]) {
                co.yield(1)
            }

            var co = Coroutine.build(done)
            var lst = [co.resume(), co.resume()]

            if lst != [Some(1), None]: {
                raise Exception("Failed.")
            }
        """)

        # incorrect (resume running coroutine)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            var v: Option[Integer] = None

            define running(co: Coroutine[Integer, Unit]): Integer {
                v = co.resume()
                return 1
            }

            var co = Coroutine.build(running).resume()

            if v != None: {
                raise Exception("Failed.")
            }
        """)

        # incorrect (receive from another coroutine)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define recv_other(co_: Coroutine[Integer, Unit]): Integer {
                return 1
            }

            try: {
                Coroutine.build(recv_other).receive()
            except RuntimeError as e:
                if e.message != "Attempt to receive a value from another coroutine.": {
                    raise Exception("Failed.")
                }
            }
        """)

        # incorrect (yield from another coroutine)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define yield_other(co_: Coroutine[Integer, Unit]): Integer {
                return 1
            }

            try: {
                Coroutine.build(yield_other).yield(1)
            except RuntimeError as e:
                if e.message != "Cannot yield from another coroutine.": {
                    raise Exception("Failed")
                }
            }
        """)

        # incorrect (yield in foreign call)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define yield_foreign(co_: Coroutine[Integer, Unit]): Integer {
                var lst = [1, 2, 3]
                lst.each(|e| co_.yield(e) )
                return 1
            }

            if Coroutine.build(yield_foreign).resume().is_some(): {
                raise Exception("Failed.")
            }
        """)
    }

    public define test_status
    {
        var t = Interpreter()

        # status (.is_done on all states)

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define fn: Coroutine[Integer, Unit] {
                define fn2(co: Coroutine[Integer, Unit]): Integer {
                    return 1
                }

                var co = Coroutine.build(fn2)
                co.resume()

                return co
            }

            var co = fn()
            var lst = [co.is_done(), co.is_failed(), co.is_running(), co.is_waiting()]

            if lst != [true, false, false, false]: {
                raise Exception("Failed.")
            }
        """)

        # status (.is_failed on all states)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define fn: Coroutine[Integer, Unit] {
                define fn2(co: Coroutine[Integer, Unit]): Integer {
                    return 0 / 0
                }

                var co = Coroutine.build(fn2)
                co.resume()

                return co
            }

            var co = fn()
            var lst = [co.is_done(), co.is_failed(), co.is_running(), co.is_waiting()]

            if lst != [false, true, false, false]: {
                raise Exception("Failed.")
            }
        """)

        # status (.is_running on all states)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define fn: List[Boolean] {
                var out: List[Boolean] = []

                define fn2(co: Coroutine[Integer, Unit]): Integer {
                    out = [co.is_done(), co.is_failed(), co.is_running(), co.is_waiting()]
                    return 0 / 0
                }

                var co = Coroutine.build(fn2)
                co.resume()

                return out
            }

            if fn() != [false, false, true, false]: {
                raise Exception("Failed.")
            }
        """)

        # status (.is_waiting on all states)

        t = Interpreter()
        assert_parse_string(t, """
            import (Coroutine) coroutine

            define fn: Coroutine[Integer, Unit] {
                define fn2(co: Coroutine[Integer, Unit]): Integer {
                    return 1
                }

                var co = Coroutine.build(fn2)

                return co
            }

            var co = fn()
            var lst = [co.is_done(), co.is_failed(), co.is_running(), co.is_waiting()]

            if lst != [false, false, false, true]: {
                raise Exception("Failed.")
            }
        """)
    }

    public define test_error_capture_states
    {
        var t = Interpreter()

        # Coroutine errors use vm error machinery, so these tests do not go into
        # detail checking exception values, only that they are reasonable.
        # Capture internal vm error (not from raise)

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define cap_int_error(co: Coroutine[Integer, Unit]): Integer {
                return 0 / 0
            }

            {
                var co = Coroutine.build(cap_int_error)
                var ok = false

                co.resume()

                with co.error() as Some(s): {
                    with s as DivisionByZeroError(e): {
                        if e.traceback.size() == 1 &&
                            e.message.size() > 1: {
                            ok = true
                        }
                    }
                }

                if ok == false: {
                    0/0
                }
            }
        """)

        # Capture raised error

        assert_parse_string(t, """
            define cap_raise_error(co: Coroutine[Integer, Unit]): Integer {
                raise ValueError("test")
            }

            {
                var co = Coroutine.build(cap_raise_error)
                var ok = false

                co.resume()

                with co.error() as Some(s): {
                    with s as ValueError(e): {
                        if e.traceback.size() == 1 &&
                            e.message == "test": {
                            ok = true
                        }
                    }
                }

                if ok == false: {
                    0/0
                }
            }
        """)

        # Check nested error and no error clear on (attempted) resumption

        assert_parse_string(t, """
            define run_three
            {
                raise IndexError("oops")
            }

            define run_two
            {
                run_three()
            }

            define cap_nested_raise(co: Coroutine[Integer, Unit]): Integer {
                run_two()
                return 1
            }

            {
                var co = Coroutine.build(cap_nested_raise)

                for i in 0...3: {
                    var ok = false

                    co.resume()

                    with co.error() as Some(s): {
                        with s as IndexError(e): {
                            if e.message == "oops" &&
                                e.traceback.size() == 3: {
                                ok = true
                            }
                        }
                    }

                    if ok == false: {
                        0/0
                    }
                }
            }
        """)

        # Coroutine that captured the error and continued

        assert_parse_string(t, """
            define cap_caught_error(co: Coroutine[Integer, Unit]): Integer {
                try: {
                    0/0
                except DivisionByZeroError:
                    0
                }

                co.yield(1)
                return 2
            }

            {
                var co = Coroutine.build(cap_caught_error)

                co.resume()

                if co.error().is_some(): {
                    0/0
                }

                co.resume()

                if co.error().is_some(): {
                    0/0
                }
            }
        """)

        # Coroutine caught the wrong error

        assert_parse_string(t, """
            define wrong_2
            {
                try: {
                    0/0
                except IndexError:
                    0
                }
            }

            define cap_wrong_error(co: Coroutine[Integer, Unit]): Integer {
                wrong_2()

                return 1
            }

            {
                var co = Coroutine.build(cap_wrong_error)
                var ok = false

                co.resume()

                with co.error() as Some(s): {
                    with s as DivisionByZeroError(e): {
                        if e.message.size() != 0 &&
                            e.traceback.size() == 2: {
                            ok = true
                        }
                    }
                }

                if ok == false: {
                    0/0
                }
            }
        """)
    }

    public define test_error_capture_with_no_error
    {
        var t = Interpreter()

        # Check error on done Coroutine that never failed

        assert_parse_string(t, """
            import (Coroutine) coroutine

            define done_fn(co: Coroutine[Integer, Unit]): Integer {
                return 1
            }

            {
                var co = Coroutine.build(done_fn)

                co.resume()

                if co.error().is_some(): {
                    0/0
                }
            }
        """)

        # Error on a running Coroutine that never failed

        assert_parse_string(t, """
            define running_fn(co: Coroutine[Integer, Unit]): Integer {
                co.yield(co.error().is_some().to_i())
                return 0
            }

            {
                var co = Coroutine.build(running_fn)
                var ok = false

                if co.resume().unwrap() != 0: {
                    0/0
                }
            }
        """)

        # Error on a waiting Coroutine that never failed

        assert_parse_string(t, """
            define waiting_fn(co: Coroutine[Integer, Unit]): Integer {
                co.yield(1)
                return 0
            }

            {
                var co = Coroutine.build(waiting_fn)
                var ok = false

                co.resume()

                if co.error().is_some(): {
                    0/0
                }
            }
        """)
    }
}
