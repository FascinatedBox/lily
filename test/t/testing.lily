#[
    This file is part of the interpreter's testing suite. It's a simplified
    version of what's available in lily-testkit. This version offloads more to
    the C side. The less Lily code involved in running the testing suite, the
    better.
]#

# The backbone library is in the same directory as this file. However, each
# test file is run in a separate interpreter as the main source to be parsed. As
# a result, this import has to backtrack out of that directory to where it
# started.
import "../t/backbone"

class TestAssertionFailed(message: String) < Exception(message) {  }
class TestSkipped(message: String) < Exception(message) {  }

# When an error is raised, the traceback includes frames from the driver.
constant trace_overhead = 3

class TestCase < backbone.TestCaseBase
{
    private var @fake_error = Exception("")

    # Since there's a lot of commonality between asserts, these are the assert
    # helpers. Every assert method needs to raise through one of these, so that
    # the traceback slicer (test_error) can consistently slice 2 frames off the
    # other end of traceback.
    private define send_assert_direct(message: String) {
        raise TestAssertionFailed(message)
    }

    private define check_fail_assert(status: Boolean,
                                     interp: backbone.Interpreter,
                                     header: String,
                                     expect: String)
    {
        if status == true: {
            var message = "Assertion ({}) failed.\n".format(header)
            raise TestAssertionFailed(message)
        else:
            var reason = interp.error()
                               .rstrip(" \n")
                               .replace("\n", "\n            ")
            expect = expect.rstrip(" \n")
                           .replace("\n\n", "\n            \n")

            if expect != reason: {
                var message = """\
                    Assertion ({}) failed incorrectly.\n\
                    (expected)`\n            {}\n`\n\
                    (received)`\n            {}\n`"""
                    .format(header, expect, reason)
                raise TestAssertionFailed(message)
            else:
                @pass_count += 1
            }
        }
    }

    public define assert_equal[A](left: A, right: A) {
        if left != right: {
            send_assert_direct("""\
                Assertion (left == right) failed.\n     \
                  left: `{}`\n    \
                 right: `{}`\
            """.format(left, right))
        else:
            @pass_count += 1
        }
    }

    public define assert_near_equal(left: Double,
                                    right: Double,
                                    delta: *Double=0.0000001) {
        var is_near = false

        if left == 0.0 && right == 0.0 ||
           ((left - right) / right < delta): {
            is_near = true
        }

        if is_near == false: {
            send_assert_direct("""\
                Assertion (left near right) failed.\n     \
                  left: `{}`\n    \
                 right: `{}`\n    \
                 delta: `{}`\n\
            """.format(left, right, delta))
        else:
            @pass_count += 1
        }
    }

    public define assert_not_equal[A](left: A, right: A) {
        if left == right: {
            send_assert_direct("""\
                Assertion (left != right) failed.\n     \
                  left: `{}`\n    \
                 right: `{}`\
            """.format(left, right))
        else:
            @pass_count += 1
        }
    }

    public define assert_false(value: Boolean) {
        if value == true: {
            send_assert_direct("Assertion (value == true) failed.")
        else:
            @pass_count += 1
        }
    }

    public define assert_true(value: Boolean) {
        if value == false: {
            send_assert_direct("Assertion (value == false) failed.")
        else:
            @pass_count += 1
        }
    }

    public define assert_parse_string(interp: backbone.Interpreter,
                                      data: String)
    {
        var result = interp.parse_string("[test]", data)

        if result == false: {
            send_assert_direct("Assertion (parse string) failed.\n" ++
                    interp.error())
        else:
            @pass_count += 1
        }
    }

    public define assert_parse_fails(interp: backbone.Interpreter,
                                     expect: String,
                                     data: String)
    {
        var result = interp.parse_string("[test]", data)

        check_fail_assert(result, interp, "parse fails", expect)
    }

    public define assert_manifest_string(interp: backbone.Interpreter,
                                         data: String)
    {
        var result = interp.parse_manifest_string("[test]", data)

        if result == false: {
            send_assert_direct("Assertion (manifest string) failed.\n" ++
                    interp.error())
        else:
            @pass_count += 1
        }
    }

    public define assert_manifest_fails(interp: backbone.Interpreter,
                                        expect: String,
                                        data: String)
    {
        var result = interp.parse_manifest_string("[test]", data)

        check_fail_assert(result, interp, "manifest fails", expect)
    }

    public define assert_render_fails(interp: backbone.Interpreter,
                                      expect: String,
                                      data: String)
    {
        var result = interp.render_string("[test]", data).is_some()

        check_fail_assert(result, interp, "render fails", expect)
    }

    public define expect_render_string(interp: backbone.Interpreter,
                                       data: String,
                                       expect: String)
    {
        var result = interp.render_string("[test]", data)

        if result.is_none(): {
            send_assert_direct("Assertion (render string) failed.\n" ++
                    interp.error())
        else:
            var given = result.unwrap()

            if expect != given: {
                send_assert_direct("""\
                    Assertion (render string) rendered incorrectly.\n\
                    (expected)`{}`\n\
                    (received)`{}`""".format(expect, given))
            else:
                @pass_count += 1
            }
        }
    }

    private define error_header(ex: Exception): String {
        var ex_name = (ex ++ "").lstrip("<").split(" ")[0]
        var ex_message = ex_name ++ ": " ++ ex.message

        return ex_message
    }

    public define assert_raises(expect: String, fn: Function()) {
        var no_error = false

        try: {
            fn()
            no_error = true
        except Exception as ex:
            var e_header = error_header(ex)

            if e_header != expect: {
                send_assert_direct("""\
                    Assertion (raises) failed.\n    \
                      expected: `{}`\n       \
                         given: `{}`\
                """.format(expect, e_header))
            }
        }

        if no_error: {
            send_assert_direct("""\
                Assertion (raises) failed.\n    \
                  expected: `{}`\n       \
                     given: (no error raised)\
            """.format(expect))
        }
    }

    public define skip(reason: String) {
        raise TestSkipped(reason)
    }

    private define test_error(error: Exception): String {
        # These first few frames are overhead from the test suite itself.
        var trace = error.traceback.slice(trace_overhead)

        match error: {
            case TestAssertionFailed(e):
                # All asserts go through a private function that does the raise,
                # so clip off two frames.
                trace = trace.slice(0, -2)
            else:
                # This is a raw exception, so keep everything.
        }

        trace = trace.map(|t| ("    from " ++ t) )

        # Fetch class name through interpolation.
        var name = (error ++ "").lstrip("<").split(" ")[0]
        var result = name ++ "\n" ++
                     error.message ++ "\n" ++
                     trace.join("\n")

        return result
    }

    # This should only be called from the backbone.
    protected define run_one_test[A](value: A, func: Function(A),
            name: String) {
        var error = @fake_error

        stdout.write("    test {}\r".format(name))
        stdout.flush()

        try: {
            func(value)
        except TestSkipped as e:
            @skip_count += 1
        except Exception as e:
            @fail_count += 1
            error = e
        }

        stdout.write("                                                            \r")
        stdout.flush()

        if error != @fake_error: {
            error |> test_error |> stderr.print
        }
    }
}

enum ImportTarget {
    StringTarget(:path String,
                 :data String),
    PackageTarget(:path String,
                  :data String),
    LibraryTarget(:path String,
                  :redirect String)
}

class Interpreter < backbone.Interpreter
{
    private var @targets: List[ImportTarget] = []

    private define do_import_hook(want_path: String)
    {
        for t in @targets: {
            match t: {
                case StringTarget(t_path, t_data):
                    if want_path == t_path: {
                        import_use_local_dir("")
                        import_string(want_path, t_data)
                        break
                    }
                case PackageTarget(t_path, t_data):
                    if want_path == t_path: {
                        import_use_package_dir("")
                        import_string(want_path, t_data)
                        break
                    }
                case LibraryTarget(t_path, t_redirect):
                    if want_path == t_path: {
                        import_use_local_dir("")
                        import_library(t_redirect)
                        break
                    }
            }
        }
    }

    private static define hook(s: backbone.Interpreter, target: String)
    {
        match s: {
            case Interpreter(i):
                Interpreter.do_import_hook(i, target)
            else:
        }
    }

    public static define with_targets(targets: ImportTarget...): Interpreter
    {
        var t = Interpreter()

        t.set_hook(hook)
        t.targets = targets

        return t
    }
}
