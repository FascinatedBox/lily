import (Coroutine) coroutine

define factorial_co(co: Coroutine[Integer, Unit], num: Integer): Integer {
    var f = 1

    for i in 2...num: {
        co.yield(f)
        f *= i
    }

    return f
}

define create_tasks(source: List[Integer]): List[Coroutine[Integer, Unit]]
{
    print("Creating {} tasks:".format(source.size()))

    for i, s in source: {
        print("Task {} will last {} rounds.".format(i, s))
    }

    var result = source.map(|m| Coroutine.build_with_value(factorial_co, m))

    return result
}

var task_list = create_tasks([
    2,
    4,
    1,
    3,
])
var total = task_list.size()
var round = 1

while 1: {
    var results = task_list.map(Coroutine.resume)
    var remaining = results.select(Result.is_success)
                           .size()

    if remaining == 0: {
        break
    }

    print("\nRound {} with {} tasks.".format(round, remaining))

    for i, r in results: {
        with r as Success(s): {
            print("Task {}: factorial({}) = {}".format(i, round, s))
        }
    }

    round += 1
}
